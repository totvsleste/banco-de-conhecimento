#INCLUDE "PROTHEUS.CH"
#INCLUDE "CTBXFUNB.CH"

Static __lSeqCorr
Static nTamDiaCtB 	:= NIL
Static lCTBRDIACTB 	:= .f.
Static cSegOFi 		:= NIL

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณUsaSeqCorrบAutor  ณRenato F. Campos    บ Data ณ  13/07/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Fun็ใo de verifica็ใo da funcionalidade de sequencia de    บฑฑ
ฑฑบ          ณ correlativo                                                บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ SIGACTB                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function UsaSeqCor()
Local lUtiliza	:= .F.

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณApenas faz a valida็ใo apenas uma vez na chamada daณ
//ณfun็ใo para melhorar a performance                 ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If ValType(__lSeqCorr) == "U"

	If CtbSegOFi() != "0"
		lUtiliza := .T.
	Endif

	__lSeqCorr := lUtiliza

Else
	lUtiliza := __lSeqCorr
EndIf

Return lUtiliza

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณVldCodSeq บAutor  ณRenato F. Campos    บ Data ณ  13/07/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Fun็ใo de valida็ใo do codigo de Diario(Sequencia)         บฑฑ
ฑฑบ          ณ  do correlativo                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ SIGACTB                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function VldCodSeq( cCodSeq )

Local aSaveArea := GetArea()
Local lRet		:= .T.
Local lSeqCorr := UsaSeqCor() 

Local cCodAux	:= CtbRDia()
Local lCt5Dia	:= X3OBRIGAT( "CT5_DIACTB" )

Default cCodSeq := &(ReadVar()) 

cSegofi  := CtbSegOFi()

IF !Empty( cCodSeq ) .And. lSeqCorr .And. cCodAux != cCodSeq  
	DbSelectArea( "CVL" )
	DbSetOrder(1)
   
   If cCodSeq $ "01/02/03/04/06/07/08/09"
		Help( " ", 1, "RESCODDIA" )
		lRet := .F.
	EndIf         
	
	If lRet .And. ! MsSeek( xFilial( "CVL" ) + cCodSeq )
		lRet := .F.
		Help( " ", 1, "NOCODDIA" )
	EndIf
EndIF

If cSegofi == '5'.and. !lCt5Dia .And. Empty( cCodSeq ) 
	lRet := .F.
	Help("",1,"OBRIGAT")
EndIf  

RestArea(aSaveArea)

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณ CTBSQCor  ณ Autor ณ Lucas			    ณ Data ณ 07/01/03 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณRetornar o proximo Nฃmero de Sequencia Correlativa.         ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณSintaxe   ณ ExpC1:= CtbSQCor()   	               			   		  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณRetorno   ณ ExpC1 = Lancamento Padrao (default CT5)                    ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ Uso      ณ SigaCTB                                                    ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Function CTBSQCor( cLancPad, cCodSeq, dDataLanc )

Local aSaveArea := GetArea()
Local cTipoSeq  := ALLTRIM(GetNewPar( "MV_SEQCORR", "1AN"))
Local cSegofi	 := ""
Local dDtIni	 := Stod( "" )
Local dDtFim	 := Stod( "" )

Local cNewSeq   := STRZERO(1,TamSX3("CVM_SEQULT")[1])

DEFAULT cLancPad 	:= ""
DEFAULT cCodSeq  	:= ""
DEFAULT dDataLanc := dDataBase

IF !UsaSeqCor() 
	Return cNewSeq
Endif

IF ValType( cCodSeq ) == "L"
	Help(" ",1,"CT5SCOR1",,STR0001,1,0)//"Controle do n๚mero correlativo desatualizado. Atualizar ambiente Contabilidade Gerencial"
	RETURN cNewSeq
ENDIF

// Efetua a troca do SX5 para CVM
ChgX5Corr( cTipoSeq, cLancPad, dDataLanc)

cSegofi  := CtbSegOFi()

If cSegOfi $ '1/2/3/4/6' 
	// Retorna o c๓digo do diario (sequencia) associado o paramentro
	cCodSeq := CtbRdia(cLancPad)
	
	// Retorna o periodo da nova linha a ser criada na tabela CVM para o controle de sequencia
	SeqCorrPer( cSegofi, dDataLanc, @dDtIni, @dDtFim,cTipoSeq )
	
	// verifica se o segofi jแ existe na CVM
	IF !Empty(dDtIni) .And. !ExistCVM( xFilial( "CVM" ) + cCodSeq + DToS(dDtIni) , 1 )
		GetNewCVM( cSegofi , dDataLanc ,dDtIni,dDtFim,cCodSeq,cTipoSeq )
	Endif
Else
	IF ! ExistCVM( xFilial( "CVM" ) + cSegofi , 3 ) .And. cCodSeq $ '05|08'
 		GetNewCVM( cSegofi ,dDataLanc,dDtIni,dDtFim,cCodSeq )
	Endif	
EndIf

If !Empty(dDtIni) .Or.  cSegOfi $ '5/7/8' 
	If cSegOfi == "8"                                      
		SeqCorrPer( cSegofi, dDataLanc, @dDtIni, @dDtFim,cTipoSeq )
	Endif	
	
	If cSegOfi == "7" .AND. Empty(cCodSeq)
		cCodSeq := CtbRdia(cLancPad)
	Endif 
			
	cNewSeq := CTBGeraSeq( cCodSeq, dDataLanc, .F.)
		
Else
	cNewSeq := ""
EndIf

RestArea(aSaveArea)

Return cNewSeq

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณ CTBSQGrv  ณ Autor ณ Lucas			    ณ Data ณ 07/01/03 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณ Efetua a gravacao do correlativo controlando a concorrenciaณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณSintaxe   ณ CtbSQGrv(     )      	               					  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ Uso      ณ SigaCTB                                                    ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ                     
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Function CTBSQGrv( cSeqCorr,dDataLanc )

Local aSaveArea 	:= GetArea()

Local cCodSeq		:= Substr( cSeqCorr , 1, 2 )

DEFAULT cSeqCorr := ""
DEFAULT dDataLanc:=dDataBase

IF !UsaSeqCor() 
	Return .F.
Endif

If !Empty(cSeqCorr)
	dbSelectArea("CT2")
	CT2->(dbSetOrder(16)) // CT2_FILIAL+CT2_NODIA
	cSegofi  := CtbSegOFi()
	
	cSeqCorr := CTBGeraSeq( cCodSeq, dDataLanc, .T.)
	
	While MsSeek( xFilial("CT2")+cSeqCorr ) .or.;//verifica se ja foi gravado
	    	 !MayIUseCode(xFilial("CT2")+cSeqCorr)  //verifica se esta na memoria, sendo usado
	        // busca o proximo numero disponivel 
			cSeqCorr := CTBGeraSeq( cCodSeq, dDataLanc, .T.)                                                                                                         	
	EndDo
	FreeusedCode(.T.)
EndIf
	
RestArea( aSaveArea )
Return cSeqCorr


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณExistCVM  บAutor  ณMicrosiga           บ Data ณ  14/07/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณVerifica se a chave existe na tabela CVM                    บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Function ExistCVM( cChave, nOrdem )
Local aArea := GetArea()
Local lRet	:= .F.	

Default cChave	:= ''
Default nOrdem	:= 0

dbSelectArea( "CVM" )
dbSetOrder( nOrdem )

lRet := MsSeek( cChave )

RestArea( aArea )

Return lRet             

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณChgX5Corr บAutor  ณRenato F. Campos    บ Data ณ  14/07/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Rotina de troca do SX5 para CVM                            บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ SIGACTB                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Function ChgX5Corr(cTipoSeq, cLancPad, dDataLanc)

Local aArea 	 	:= GetArea()
Local cChaveTab 	:= CTBSQChv(cTipoSeq, cLancPad,dDataLanc)
Local cPeriodo		:= Substr(cTipoSeq,2,1) 
Local cTipo			:= Substr(cTipoSeq,1,1)
Local cSeqIni		:= ""
Local dDtIni		:= STod("")
Local dDtFim		:= STod("")  


DbSelectArea( "SX5" )
DbSetOrder( 1 )


If SX5->(MsSeek( xFilial( "SX5" ) + "SC" + cChaveTab ))
	//Defini็ใo do tipo de controle configurado pelo parametro "MV_SEQCORR"
	If cTipo == '2' // Controle por comprovante
		cSegNew := '6'	
	Else // Sequencial
		If UPPER(cPeriodo) == "M"
			cSegNew := '2'	
		Else
			cSegNew := '3'	
		EndIf
	EndIf

	// Ajusta o parametro "MV_SEGOFI" 
	PutMv("MV_SEGOFI",cSegNew)
	Help(" ",1,"CT5SCOR2",,STR0002 + cSegNew + STR0003,1,0)//"O parametro MV_SEGOFI serแ trocado para " ## " para manter a integridade dos c๓digos correlativos antigos"
	
	cSeqIni := Soma1( Alltrim( X5Descri() ), TamSx3("CVM_SEQULT")[1]) 
	// Retorna o c๓digo do diario (sequencia) associado o paramentro
	cCodSeq := CtbRdia(cLancPad)
	
	// Retorna o periodo da nova linha a ser criada na tabela CVM para o controle de sequencia
	SeqCorrPer( cSegNew, dDataLanc, @dDtIni, @dDtFim,cTipoSeq )
	
	// verifica se o segofi jแ existe na CVM
	IF !Empty(dDtIni) .And. !ExistCVM( xFilial( "CVM" ) + cCodSeq + DToS(dDtIni) , 1 )
		GetNewCVM( cSegNew , dDataLanc ,dDtIni,dDtFim,cCodSeq,cTipoSeq,cSeqIni )
	Endif

EndIf

If SX5->(MsSeek( xFilial( "SX5" ) + "SC" + cChaveTab ))
	RecLock( "SX5", .F. )
		SX5->(DbDelete())
	MsUnLock()
EndIf
RestArea( aArea )

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFunใo    ณ CTBSQChv  ณ Autor ณ Leonardo             ณ Data ณ 14/01/03 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescri็ใo ณRetornar a chave da tabela SC conforme MV_SEQCORR           ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณSintaxe   ณ CtbSQChv(ExpC1)      	               					  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณParametro ณ ExpC1 = Tipo de sequencia (MV_SEQCORR)                     ณฑฑ
ฑฑณ          ณ ExpC2 = Lancamento Padrao                                  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ Uso      ณ SigaCTB                                                    ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Function CTBSQChv( cTipoSeq, cLancPad,dDataLanc )
Local cChave 
Local cTpCorr, cTpZera
Local cSeqLan001 := GetNewPar( "MV_SQSUB01", "500|501|502|563|565|575|576|581")
Local cSeqLan002 := GetNewPar( "MV_SQSUB02", "510|513|514|560|561|562|564|570|571|590|591|597")
Local cSeqLan003 := GetNewPar( "MV_SQSUB03", "")
             
Default cTipoSeq := ALLTRIM(GetNewPar( "MV_SEQCORR", "1AN"))
Default dDataLanc := dDataBase 

cTpCorr := Subs( cTipoSeq, 1, 1)  // "1" ou "2"
cTpZera := Subs( cTipoSeq, 2, 1)  // "M", "A" ou diferente (nao zera)
    
cChave := "" 
If cTpZera = "M"
	cChave := Subs( DtoS( dDataLanc ), 3, 4) //"yymm" - chave mensal

ElseIf cTpZera = "A"
    cChave := Subs( DtoS( dDataLanc ), 3, 2) //"yy" - chave anual
EndIF

If cTpCorr = "1"
	//UNICO 
 	cChave := cChave + "0"
Else 
    // CLASSIFICA POR TIPO DE COMPROVANTE (INGRESO, EGRESO OU TRASPASO)
	If AllTrim(cLancPad) $ cSeqLan001
		cChave := cChave + "1"
	ElseIf AllTrim(cLancPad) $ cSeqLan002
		cChave := cChave + "2"
	ElseIf Empty(cSeqLan003) .Or. AllTrim(cLancPad) $ cSeqLan003
		cChave := cChave + "3"
	Else
	   cChave := cChave + "0"
	EndIf                       
EndIf

Return( cChave )           


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGetNewCVM บAutor  ณRenato F. Campos    บ Data ณ  07/14/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Cria um novo registro na tabela CVM conforme parametro     บฑฑ
ฑฑบ          ณ MV_SEGOFI                                                  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function GetNewCVM( cSegofi ,dDataLanc ,dDtIni,dDtFim,cCodSeq,cTipoSeq,cSeqIni )
Local aCVM			:= {}
Local cExerc		:= Substr( DtoS( dDataLanc ), 1, 4)
Local cSeqFim  	:= ""
Local cSeqAux		:= ""
Local cCalend		:= Posicione("CTG", 4, xFilial("CTG") + cExerc, "CTG_CALEND" )
Local aArea			:= GetArea()
Local cTitulo		:= ""
Local cPeriodo		:= ""

Default cTipoSeq 		:= ALLTRIM(GetNewPar( "MV_SEQCORR", "1AN"))
Default cSeqIni   	:= STRZERO(1,TamSX3("CVM_SEQULT")[1])

cRad := LastSeqCVM(cCodSeq,"CVM_RAD")
cRad := IIF(!Empty(cRad),Soma1(cRad),StrZero(1,TamSX3("CVM_RAD")[1]))	

Do Case
	Case cSegOfi == '1'
		cTitulo := STR0006 //"Controle sequencial continuo"
		dDtIni  := StoD( cExerc + "0101" )
		dDtFim  := StoD( "  /  /  " )
		cSeqAux := LastSeqCVM(cCodSeq,"CVM_SEQULT")
		cSeqIni:= If(!Empty(cSeqAux) , Soma1(cSeqAux),cSeqIni)
	Case cSegOfi == '2'
		cTitulo := STR0007 //"Controle sequencial no mes"
	Case cSegOfi == '3|8'
		cTitulo := STR0008 // "Controle sequencial no ano"
	Case cSegOfi == '4'
		cTitulo := STR0009 //"Controle sequencial no Periodo"
	Case cSegOfi == '6'
		If cCodSeq == "07"
			cTitulo := STR0010 // "Controle sequencial Configurado Ingresso "
		ElseIf cCodSeq == "08"
			cTitulo := STR0011 // "Controle sequencial Configurado Egresso "
		ElseIf cCodSeq == "09"
			cTitulo := STR0012 // "Controle sequencial Configurado Transpasso "
		Else
			cTitulo := STR0013 // "Controle sequencial Configurado Normal "
		EndIf
		
		cPeriodo:= SubStr(cTipoSeq,2,1)
		If !(cPeriodo $ "A/M") // Caso o parametro esteja inconsistente, o sequencial serแ continuo
			cSeqAux := LastSeqCVM(cSegOfi,"CVM_SEQULT")
			cSeqIni:= If(!Empty(cSeqAux) , Soma1(cSeqAux),cSeqIni)
		EndIf
		
	OtherWise
		cTitulo := STR0014 // "Controle sequencial no Configurado"
		cCalend := cExerc
		dDtIni  := StoD( cExerc + "0101" )
		dDtFim  := StoD( "  /  /  " )
EndCase

If !Empty(cTitulo)
	aCVM := { xFilial( "CVM" );
				, cCodSeq ; 
				, cTitulo;
				, cSegOfi;
				, dDtIni;
				, dDtFim;
				, StoD( "  /  /  " );
				, cSeqIni;
				, cSeqFim;
				, 1;
				, cRad;
				, cCalend;
				}
ENDIF

IF Len( aCVM ) > 0
	CreateCvm( aCVM )
Endif

RestArea(aArea)
Return


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณLastSeqCVMบAutor  ณAlvaro Camillo Neto บ Data ณ  11/08/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Retorna a ultima sequencia do diแrio                       บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ SIGACTB                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function LastSeqCVM(cCodSeq,cCpo)
Local cRet := ""
Local aArea:= GetArea()

dbSelectArea("CVM")
CVM->(dbSetOrder(1)) //CVM_FILIAL+CVM_COD+CVM_DTINI+CVM_RAD
If CVM->(MsSeek(xFilial("CVM") + cCodSeq ))
	While  CVM->(CVM_FILIAL+CVM_COD) == xFilial("CVM") + cCodSeq .And. CVM->(!EOF())
		cRet := CVM->&(cCpo) 
		CVM->(dbSkip())
	EndDo 
EndIf 

RestArea(aArea)
Return cRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCreateCvm บAutor  ณRenato F. Campos    บ Data ณ  14/07/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRotina de cria็ใo da tabela CVM para controle do correlativoบฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบEstrutura ณaCVM[01]	=	CVM_FILIAL                                    บฑฑ
ฑฑบ          ณaCVM[02]	=	CVM_COD	                                      บฑฑ
ฑฑบ          ณaCVM[03]	=	CVM_DESCR                                     บฑฑ
ฑฑบ          ณaCVM[04]	=	CVM_TIPOSE                                    บฑฑ
ฑฑบ          ณaCVM[05]	=	CVM_DTINI	                                  บฑฑ
ฑฑบ          ณaCVM[06]	=	CVM_DTFIM	                                  บฑฑ
ฑฑบ          ณaCVM[07]	=	CVM_DTSEQ	                                  บฑฑ
ฑฑบ          ณaCVM[08]	=	CVM_SEQINI                                    บฑฑ
ฑฑบ          ณaCVM[09]	=	CVM_SEQULT                                    บฑฑ
ฑฑบ          ณaCVM[10]	=	CVM_INCRE	                                  บฑฑ
ฑฑบ          ณaCVM[11]	=	CVM_RAD		                                  บฑฑ
ฑฑบ          ณaCVM[12]	=	CVM_CALEND	                                  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ SIGACTB                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function CreateCvm( aCVM )
Local aArea := GetArea()

DbSelectArea("CVM" )
DbSetOrder( 1 ) //CVM_FILIAL+CVM_COD+CVM_DTINI+CVM_RAD

IF ! MsSeek( aCVM[01] + aCVM[02] + DtoS(aCVM[05]) + aCVM[11] )
	
	GravaCVM(aCVM)

Endif	

DbSelectArea( "CVL" )
DbSetOrder( 1 )
IF ! MsSeek( aCVM[1] + aCVM[2] )
	GravaCVL( aCVM )
Endif

RestArea( aArea )

Return


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณSeqCorrPerบAutor  ณRenato F. Campos    บ Data ณ  14/07/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Atualiza a data inicial e final do periodo conforme o tipo บฑฑ
ฑฑบ          ณ correlativo                                                บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function SeqCorrPer(cSegofi, dDataCorr, dDtIni, dDtFim,cTipoSeq )  
Local cAnoExe := "1980"
Local cMesExe := "01"
Local cPeriodo := ""
Local cAliasQry := GetNextAlias()

Default dDataCorr := dDataBase
Default cTipoSeq := ALLTRIM(GetNewPar( "MV_SEQCORR", "1AN"))

cAnoExe  := Substr( DtoS( dDataCorr ), 1, 4)
cMesExe  := SubStr( DtoS( dDataCorr ), 5, 2)

// Efetua a gera็ใo do proximo numero
If cSegofi == "2" // Mensal
	dDtIni := FirstDay(dDataCorr)
	dDtFim := LastDay(dDataCorr)	
ElseIf cSegofi $ "3" // Anual
	dDtIni := Stod( cAnoExe + "0101" )
	dDtFim := Stod( cAnoExe + "1231" )
ElseIf cSegofi == "4" //Periodo

	BeginSQL Alias cAliasQry	
		SELECT 
			MIN(CTG.CTG_DTINI) DTINI,MAX(CTG.CTG_DTFIM) DTFIM 
		FROM 
			%Table:CTG% CTG 
		WHERE 
				CTG.CTG_FILIAL = %Exp:xFilial("CTG")%
			AND	CTG.CTG_EXERC = %Exp:cAnoExe% 
			AND CTG.%NotDel%	
	EndSQL

	If !Empty((cAliasQry)->DTINI) .And. !Empty((cAliasQry)->DTFIM)
		dDtIni := Stod( SubStr((cAliasQry)->DTINI,1,4) + GetNewPar( "MV_CTBCRPI", "0101") )
		dDtFim := Stod( SubStr((cAliasQry)->DTFIM,1,4) + GetNewPar( "MV_CTBCRPF", "0131") )	
	Else
		dDtIni := Stod( cAnoExe + GetNewPar( "MV_CTBCRPI", "0101") )
		dDtFim := Stod( cAnoExe + GetNewPar( "MV_CTBCRPF", "0131") )
	EndIf
	
	(cAliasQry)->(DbCloseArea())
	
	If dDataCorr < dDtIni .Or. dDataCorr > dDtFim // Estแ fora do periodo
		dDtIni := Stod( "" ) 
		dDtFim := Stod( "" )
	EndIf    
ElseIf cSegofi == "6" //Dependente do parametro
	cPeriodo := SubStr(cTipoSeq,2,1)
	If UPPER(cPeriodo) == "M" // Mensal
		dDtIni := FirstDay(dDataCorr)
		dDtFim := LastDay(dDataCorr)	
	Else
		dDtIni := Stod( cAnoExe + "0101" )
		dDtFim := Stod( cAnoExe + "1231" )
	EndIf  
Else
	dDtIni := Stod( cAnoExe + "0101" )
	dDtFim := Stod( cAnoExe + "1231" )
Endif

Return

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณCTBSubtoPadณ Autor ณ Leonardo             ณ Data ณ 07/01/03 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณRetornar algum lancamento padrao do tipo de comprovante     ณฑฑ
ฑฑณ          ณ  (quando sublote corresponde ao tipo ( MV_SEQCORR = ??S )  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณSintaxe   ณ CtbSQCor(ExpC1)      	               					  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณParametro ณ ExpC1 = Sublote configurado como tipo de comprovante       ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ Uso      ณ SigaCTB - Localizacao Chile                                ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Function CTBSubToPad( cSubLote)
Local cLanPad := ""
Local cSeqLan001 := GetNewPar("MV_SQSUB01","500|501|502|563|565|575|576|581")
Local cSeqLan002 := GetNewPar("MV_SQSUB02","510|513|514|560|561|562|564|570|571|590|591|597")
Local cSeqLan003 := GetNewPar("MV_SQSUB03","")

cTipoSeq := GetNewPar("MV_SEQCORR","1AN")

If Subs( cTipoSeq, 3, 1) = "S"

	If cSubLote = "001"
		cLanPad := Iif(Empty(cSeqLan001),"575",Substr(cSeqLan001,1,3)) // ingresos
	ElseIf cSubLote = "002"
		cLanPad := Iif(Empty(cSeqLan002),"570",Substr(cSeqLan002,1,3))//egresos
	Else 
		cLanPad := Iif(Empty(cSeqLan003),"620",Substr(cSeqLan003,1,3)) // traspasos
	EndIf       
EndIf
Return( cLanPad)

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuncao    ณ CTBVlSeqCr  ณ Autor ณ MICROSIGA	             ณ Data ณ 31/07/08 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณ Valida a inclusao de numero correlativo anterior 			   ณฑฑ
ฑฑณ          ณ 										                           ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณSintaxe   ณ CTBVlSeqCr()                                                    ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณRetorno   ณ Logico	                                                       ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณUso       ณ Generico                                                        ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณParametrosณ dDataMov - Data do movimento			                           ณฑฑ 
ฑฑณ          ณ lCapaLote- Indica se sera efetuado a validacao                  ณฑฑ 
ฑฑณ          ณ cTpSaldo - Saldo do movimento 		                           ณฑฑ 
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/

Function CTBVlSeqCr(dDataMov,lCapaLote,cTpSaldo)
Local aArea		:= GetArea()
Local lRet 	    := .T.
Local cCtbVlSq  := GetNewPar("MV_CTBVLSQ","1")

DEFAULT dDataMov  := StoD("  /  /  ")
DEFAULT cTpSaldo  := "0/1/2/3/4/9/*"
DEFAULT lCapaLote := .T.

// se usa correlativo
If UsaSeqCor()

	//Se o tipo de saldo ้ para ser validado e nao for capa de lote
	If Alltrim( cCtbVlSq ) $ cTpSaldo .and. ! lCapaLote
		
		dbSelectArea("CTF")
		dbSetOrder(1)
		
		CTF->(dbSeek(xFilial("CTF") + Dtos( dDataMov + 1 ),.T.))   
		
		If CTF->CTF_FILIAL == xFilial("CTF") .And. CTF->CTF_DATA > dDataMov
			Help( " " , 1 , "ERR_SEGOFI" ,,STR0015,3,0)
			lRet := .F.
		Endif
	Endif
EndIf

RestArea( aArea )

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออหอออออออัอออออออออออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCTBGeraSeq  บ Autor ณ SI4004 - Anderson Goncalves บ Data ณ  21/05/08   บฑฑ
ฑฑฬออออออออออุออออออออออออสอออออออฯอออออออออออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Rotina para gera็ใo do numero sequencia						         บฑฑ
ฑฑบ          ณ                                                                       บฑฑ      
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ                           	  	                                     บฑฑ
ฑฑบ          ณ																		 บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ SIGACTB			                                                     บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿ 
*/    
                                   
Function CTBGeraSeq(cCodigo, dData, lGrava)

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณVariaveis da rotina													   ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู 
Local aRegCVM       := {}
Local nX            := 0 
Local cRet          := ""
Local _cTipSeq		:= ""            
Local _cDescLvr     := ""

Default lGrava		:= .F.
Default dData		:= cToD("")

If Empty(dData)
	dData := dDataBase
EndIf

cSegofi  := CtbSegOFi()                                                   

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณAbre e seta as tabelas no indice da rotina                              ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู 
dbSelectArea("CVM")
CVM->(dbSetOrder(1)) //CVM_FILIAL+CVM_COD+DTOS(CVM_DTINI)+CVM_RAD
CVM->(MsSeek(xFilial("CVM")+cCodigo))                
	                                       
While CVM->(!EOF() .and. CVM_COD == cCodigo .And. xFilial("CVM") == CVM_FILIAL  )
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณInclui no Array os registros de mesmo codigo de diario                  ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู 
	aAdd( aRegCVM, { 	CVM->CVM_COD ,;        	//1-Codigo do Diario
					 	CVM->CVM_DTINI,;      	//2-Data Inicial
					 	CVM->CVM_RAD,;        	//3-Radical
					 	CVM->CVM_DTSEQ,;      	//4-Data Ultimo Sequencial
					 	CVM->CVM_INCRE,;      	//5-Incremento
					 	CVM->CVM_SEQINI,;     	//6-Sequencia Inicial
					 	CVM->CVM_SEQULT } )  	//7-Ultimo Sequencial
	CVM->(dbSkip())
Enddo
	
For nX := 1 To Len(aRegCVM)
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณSo entrara na condi็ใo se a data for maior ou igual a data inicial      ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If dData >= aRegCVM[nX,02]
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณVerifica se tem proximo registro dentro do Array                        ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If nX+1 <= Len(aRegCVM)
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณVerifica se o ultimo sequencial e >= ou braco para retornar .t.         ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			If ! ( dData >= aRegCVM[nX][02] .and. dData < aRegCVM[nX+1][02] )
				Loop
			Endif   
			
			 
			  
		Endif

		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณPosiciona no arquivo para salvar os campos Dt.Ult.Seq e Ultimo sequenciaณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		CVM->(MsSeek(xFilial("CVM")+aRegCVM[nX][1]+dtos(aRegCVM[nX][02])+aRegCVM[nX][3]))
		
		If !Empty(CVM->CVM_SEQULT)  
			cUltSeq := STRZERO((Val(aRegCVM[nX][07])+aRegCVM[nX][5]),TamSX3("CVM_SEQULT")[1])
		Else                                               
			cUltSeq := aRegCVM[nX][06]
		Endif                      
		
		
		If CVL->(DbSeek(xFilial("CVL") + CVM->CVM_COD)) .And. X3Usado("CVL_TIPSEQ")
			_cTipSeq 	:= CVL->CVL_TIPSEQ    
			_cDescLvr   := CVL->CVL_DESCR
		Endif 
		
	    
		If Year(dData) > Year(aRegCVM[nX][04]).And. _cTipSeq = "3" .And. cSegOfi = "8" .and. X3Usado("CVL_TIPSEQ")  // Controle para zerar por processo anualmente
			cUltSeq := aRegCVM[nX][06]
			CVM->(DbSetOrder(1))
			                                
			If .not. CVM->(DbSeek(xFilial("CVM") + aRegCVM[nX][1] + DTos(Ctod("01/01/"+ Substr(Str(Year(dData),4,0),3,2)))))
			
				cCodSeq 	:= aRegCVM[nX][1]
				cTitulo		:= _cDescLvr 
				cSegOfi		:= "8"      			 	        						
				dDtIni		:= Ctod("01/01/"+ Substr(Str(Year(dData),4,0),3,2))		
				dDtFim 		:= Ctod("31/12/"+ Substr(Str(Year(dData),4,0),3,2))
				cRad 		:= Strzero(Val(aRegCVM[nX][3]) + 1,2)						          						  							      							//6-Incremento
				cSeqIni 	:= Replicate('0', TamSx3("CVM_SEQINI")[1])     												
				cSeqfim 	:= ''
				cCalend		:= CTG->CTG_CALEND                  
				
				//armazena dados
				
				aCVM := { xFilial( "CVM" );
				, cCodSeq ; 
				, cTitulo;
				, cSegOfi;
				, dDtIni;
				, dDtFim;
				, StoD( "  /  /  " );
				, cSeqIni;
				, cSeqFim;                                     
				, 1;
				, cRad;
				, cCalend;
				}
								
				GravaCVM(aCVM) //Grava sequencial do livro para novo periodo.
				
			Else                                      		 
				cUltSeq := STRZERO((Val(aRegCVM[nX][07])+aRegCVM[nX][5]),TamSX3("CVM_SEQULT")[1])
			Endif     
		Endif
		                                               
		
		IF lGrava
			RecLock( "CVM" , .F. )
			CVM->CVM_SEQULT := cUltSeq
			CVM->CVM_DTSEQ  := dData
			msUnlock()
		Endif				                                 
		
		cRet := CVM->(CVM_COD + CVM_RAD) + cUltSeq
		
		Exit
	EndIf

Next nX
		
Return(cRet) 


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCtbRdia   บAutor  ณAlvaro Camillo Neto บ Data ณ  06/08/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณIncializador padrใo para o campo XXX_DIACTB(X3_Rela็ใo)     บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function CtbRdia(cLancPad)
Local cCodDia := ""
Local aArea := GetArea()
Local lSeqCorr := UsaSeqCor()


Default cLancPad := ""

cSegofi  := CtbSegOFi()

If nTamDiaCtB == NIL
	lCTBRDIACTB := ExistBlock("CTBRDIACTB")
	nTamDiaCtB := TamSX3("CT2_DIACTB")[1]
Endif

If lSeqCorr
	cCodDia := Space(nTamDiaCtB) 
	Do Case
		Case cSegOfi == "1"
			cCodDia := "01"  
		Case cSegOfi == "2"
			cCodDia := "02"  
		Case cSegOfi == "3"
			cCodDia := "03"  
		Case cSegOfi == "4"
			cCodDia := "04"  
		//Case cSegOfi == "5" 
		Case cSegOfi $ "5"
			If lCTBRDIACTB
				cCodDia := ExecBlock("CTBRDIACTB",.F.,.F.)
				If ValType(cCodDia) != "C"
					cCodDia := Space(nTamDiaCtB)
				EndIf
			EndIf
		Case cSegOfi == "6"
			cCodDia := CTBSeqComp(cLancPad)
		Case cSegOfi == "7" .or. cSegOfi == "8" 
			If cPaisLoc == "PTG"
				cCodDia := If(cLancPad <> "", CTBSeqProc(cLancPad), cCodDia)
			Else
				cCodDia := CTBSeqProc(cLancPad)
			EndIf
	EndCase
EndIf
RestArea(aArea) 

Return cCodDia        

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCtbWdia   บAutor  ณAlvaro Camillo Neto บ Data ณ  06/08/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณModo de edi็ใo para o campo XXX_DIACTB(X3_WHEN)             บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/ 
Function CtbWdia()

Local lRet := .F.
Local aArea := GetArea()

cSegofi  := CtbSegOFi()

DbSelectArea("CVL")

If cSegOFI $ '5|8'
	lRet := .T.
EndIf 

RestArea(aArea) 
Return lRet 

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCVLValCodบAutor  ณAlvaro Camillo Neto บ Data ณ  10/08/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRealiza a valida็ใo do c๓digo de diario na tabela CVL       บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ SIGACTB                                                   บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function CVLValCod(cCodDia)
Local lRet 		:= .T.
Local lModCorr := GetNewPar("MV_CTBLBSQ","N") == "S"
Local aArea 	:= GetArea()

Default cCodDia := M->CVL_COD

DbSelectArea("CVL")

If cCodDia $ "01/02/03/04/06/07/08/09" .And. !lModCorr
	Help( " ", 1, "RESCODDIA" )
	lRet := .F.
EndIf

If lRet
	lRet := ExistChav("CVL",cCodDia)
EndIf 

RestArea(aArea) 
Return lRet  


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCTBSeqCompบAutor  ณAlvaro Camillo Neto บ Data ณ  13/08/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRetorna o c๓digo do livro diario do comprovante de acordo   บฑฑ
ฑฑบ          ณcom o c๓digo do lan็amento padrao                           บฑฑ
ฑฑบ          ณFun็ใo utilizada quando o parametro MV_SEGOFI for 6         บฑฑ
ฑฑบ          ณCONTROLE POR TIPO COMPROVANTE                               บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function CTBSeqComp(cLancPad)
Local cCodDia := ""     
Local cSeqLan001 := GetNewPar("MV_SQSUB01","500|501|502|563|565|575|576|581")
Local cSeqLan002 := GetNewPar("MV_SQSUB02","510|513|514|560|561|562|564|570|571|590|591|597")
Local cSeqLan003 := GetNewPar("MV_SQSUB03","")

If AllTrim(cLancPad) $ cSeqLan001
	cCodDia := "07"
ElseIf AllTrim(cLancPad) $ cSeqLan002
	cCodDia := "08"
ElseIf Empty(cSeqLan003) .Or. AllTrim(cLancPad) $ cSeqLan003
	cCodDia := "09"
Else
   cCodDia := "06"
EndIf
	                       
Return cCodDia

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCTBAVerDiaบAutor  ณAnderson Goncalves  บ Data ณ  28/05/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Rotina para retornar o codigo do diario                    บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Generico                                                   บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Function CTBAVerDia()

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณVariaveis da rotina                                                     ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
Local cCodSeq := CtbRDia()
Local oCodSeq
Local aArea  := GetArea()
Local nOpc   := 0
Local cRet   := ""
Local oDlg
Local cSeq:= CTBSQCor() // Para criar o diario padrใo, caso necessแrio
Local lCt5Dia	:= X3OBRIGAT( "CT5_DIACTB" )


cSegofi  := CtbSegOFi()

If nTamDiaCtB == NIL
	nTamDiaCtB := TamSX3("CT2_DIACTB")[1]
Endif

If Empty(cCodSeq)
	cCodSeq = Space(nTamDiaCtB)	
EndIf

If !IsBlind() .And. cSegOFI $ '5|8' 
	DEFINE MSDIALOG oDlg TITLE STR0004 FROM 178,181 TO 293,337 PIXEL //"Codigo do Diario"
	
	@ 003,003 TO 038,078 LABEL "" PIXEL OF oDlg
	
	@ 008,007 Say STR0005 Size 063,008 COLOR CLR_BLACK PIXEL OF oDlg //"Informe o codigo do diario"
	@ 017,007 MSGET oCodSeq VAR cCodSeq Pict "!!" Valid VldCodSeq( cCodSeq ) F3 "CVL" SIZE 060,009 OF oDlg PIXEL HASBUTTON When CtbWdia()
	
	Define SBUTTON FROM 040, 020 Type 1 ENABLE OF oDlg Action (IIf( VldCodSeq( cCodSeq ),(nOpc := 1,oDlg:End()),))
	Define SBUTTON FROM 040, 050 Type 2 ENABLE OF oDlg Action ( IIF(lCt5Dia,(nOpc := 0,oDlg:End()),) )	
	
	ACTIVATE MSDIALOG oDlg CENTERED 
	                         
	If nOpc == 1
		cRet := cCodSeq
	Else
		cRet := ""
	EndIf
Else
	cRet := cCodSeq
EndIf	

RestArea(aArea)
Return(cRet)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออหอออออออัอออออออออออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCTBVldDiarioบ Autor ณ SI4004 - Anderson Goncalves บ Data ณ  16/05/08   บฑฑ
ฑฑฬออออออออออุออออออออออออสอออออออฯอออออออออออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Rotina para validar se o codigo de diario podera ser lancado          บฑฑ
ฑฑบ          ณ na data inicial digitada                                              บฑฑ      
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ cCodigo = M->CVL_CODIGO												 บฑฑ
ฑฑบ          ณ dData   = M->CVM_DTINI												 บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ SIGACTB			                                                     บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿ 
*/                                                                                                                                           

Function CTBVldDiario(cCodigo, dData)                                 

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณVariaveis da rotina													   ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู 
Local lRet 			:= .F.

Default dData := dDatabase

lRet := VldCodSeq( cCodigo ) 
 
Return(lRet)
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCTBSeqProcบAutor  ณAlvaro Camillo Neto บ Data ณ  01/12/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Busca o c๓digo do diแrio baseado no lan็amento padrao      บฑฑ
ฑฑบ          ณ e o processo cadastrado na tabela CTW                      บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function CTBSeqProc(cLancPad)
Local aArea 	:= GetArea()
Local cCodDia  := ""
Local cProcesso:= ""
Local cTabela	:= Alltrim( SuperGetMv("MV_CTBLVPR",.F.,"") )

//Verifica se existe a tabela cadastrata no parametro
If !Empty(cTabela)
	cProcesso := GetAdvFVal("CVA","CVA_PROCES",xFilial("CVA") + cLancPad )
	cCodDia	 := IIF( FindFunction("CW0Chav"), ALLTRIM( CW0Chav(cTabela,cProcesso,,"99") ), "")
EndIf

cCodDia := IIF( Empty( cCodDia ) , "99", cCodDia)
RestArea(aArea)
Return cCodDia

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFun็ใo    ณ CTBClcPrdบAutor  ณ Marylly A. Silva   บ Data ณ  21/11/11   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Fun็ใo de cแlculo de perํodos de acordo com periodicidade  บฑฑ
ฑฑบ          ณ definida, usando como refer๊ncia uma data de inํcio e fim  บฑฑ
ฑฑบ          ณ quebrando o perํodo informado conforme a periodicidade 	  บฑฑ
ฑฑบ          ณ informada. Perํodos possํveis (Parโmetro nPeriod):		  บฑฑ
ฑฑบ          ณ 1 - Semana												  บฑฑ
ฑฑบ          ณ 2 - Dec๊ndio												  บฑฑ
ฑฑบ          ณ 3 - Quinzena												  บฑฑ
ฑฑบ          ณ 4 - Mensal												  บฑฑ
ฑฑบ          ณ 5 - Bimestral											  บฑฑ
ฑฑบ          ณ 6 - Trimestral											  บฑฑ
ฑฑบ          ณ 7 - Semestral											  บฑฑ
ฑฑบ          ณ 8 - Anual												  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบPrograma  ณ CTBR900 - Relat๓rio de Demonstrativos Contแbeis 			  บฑฑ
ฑฑบ          ณ utilizando Multiplas Vis๕es Gerenciais 			          บฑฑ
ฑฑบ          ณ (Multi Saldos/Multi Perํodos)							  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Parโmetro @dDtIni(Data de Inํcio do Periodo de Abrang๊ncia)บฑฑ
ฑฑบ          ณ Parโmetro @dDtFim (Data de Fim do Periodo de Abrang๊ncia)  บฑฑ
ฑฑบ          ณ Parโmetro @nPeriod (Periodicidade a ser utilizada)		  บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function CTBClcPrd(dDtIni,dDtFim,nPeriod)

Local aArrPrd := {}
Local dDtInAx := CTOD("  /  /  ")
Local dDtFiAx := CTOD("  /  /  ")
Local nQtPeri := 0 // Quantidade de Perํodos (Ex. 2 Semanas, 3 Quinzenas)
Local nContPr := 0
Local lRest   := .F.

Default nPeriod	:= 5 // Padrใo "Mensal"

If nPeriod == "1" // Semana
	nQtPeri := ABS((dDtFim - dDtIni) / 7)
	lRest	:= IIf(Mod((dDtFim - dDtIni), 7) != 0,.T.,.F.)
	dDtInAx := dDtIni
	For nContPr := 1 To nQtPeri
		/* 
		 * Se Quantidade de Perํodos (Semanas) nใo for absoluta, ๚ltimo perํodo deve conter a ๚ltima data com a data final do parโmetro
		 */
		If nContPr == nQtPeri .AND. lRest
			dDtInAx := dDtFiAx + 1
			aAdd(aArrPrd,{aDtInAx,dDtFim})
		/*
		 * Se a Data Inicial for (Domingo), Perํodo de 1 dia (somente a data inicial)
		 */
		ElseIf Dow(dDtInAx) == 7 .AND. nContPr == 1
			aAdd(aArrPrd,{dDtInAx,dDtInAx})
		ElseIf Dow(dDtInAx) < 7 .AND. nContPr == 1
			dDtFiAx := dDtInAx + ( 7 - Dow(dDtInAx))
			aAdd(aArrPrd,{dDtInAx,dDtFiAx})	
		Else
			dDtInAx := dDtFiAx + 1
			dDtFiAx := dDtInAx + ( 7 - Dow(dDtInAx))
			aAdd(aArrPrd,{dDtInAx,dDtFiAx})
		EndIf			
  	Next nContPr
ElseIf nPeriod == "2" // Dec๊ndio "Perํodo - 10 dias "
	nQtPeri := ABS((dDtFim - dDtIni) / 10)
	lRest	:= IIf(Mod((dDtFim - dDtIni), 10) != 0,.T.,.F.)
	dDtInAx := dDtIni
	For nContPr := 1 To nQtPeri
		/* 
		 * Se Quantidade de Perํodos (Semanas) nใo for absoluta, ๚ltimo perํodo deve conter a ๚ltima data com a data final do parโmetro
		 */
		If nContPr == nQtPeri .AND. lRest
			dDtInAx := dDtFiAx + 1
			aAdd(aArrPrd,{aDtInAx,dDtFim})
		/*
		 * Se a Data Inicial for Final de Dec๊ndio, Perํodo de 1 dia (somente a data inicial)
		 */
		ElseIf Day(dDtInAx) $ '10|20|30' .AND. nContPr == 1
			dDtFiAx := dDtInAx
			aAdd(aArrPrd,{dDtInAx,dDtFiAx})
			dDtInAx := dDtInAx + 1
		Else
			dDtInAx := dDtFiAx + 1
			dDtFiAx := dDtInAx + 10
			If PADL(Day(dDtFiAx),2,"0") $ '28|29|30|31'
				dDtFiAx := LastDay(dDtFiAx)
			EndIf
			aAdd(aArrPrd,{dDtInAx,dDtFiAx})
		EndIf			
  	Next nContPr
ElseIf nPeriod == "3" // Quinzena "Perํodo de 15 dias "
	nQtPeri := ABS((dDtFim - dDtIni) / 15)
	lRest	:= IIf(Mod((dDtFim - dDtIni), 15) != 0,.T.,.F.)
	dDtInAx := dDtIni
	For nContPr := 1 To nQtPeri
		/* 
		 * Se Quantidade de Perํodos (Quizenas) nใo for absoluta, ๚ltimo perํodo deve conter a ๚ltima data com a data final do parโmetro
		 */
		If nContPr == nQtPeri .AND. lRest
			dDtInAx := dDtFiAx + 1
			aAdd(aArrPrd,{aDtInAx,dDtFim})
		/*
		 * Se a Data Inicial for Final de Quinzena, Perํodo de 1 dia (somente a data inicial)
		 */
		ElseIf PADL(Day(dDtInAx),2,"0") $ '15|30' .AND. nContPr == 1
			dDtFiAx := dDtInAx
			aAdd(aArrPrd,{dDtInAx,dDtFiAx})
			dDtInAx := dDtInAx + 1
		ElseIf nContPr == 1
			dDtFiAx := dDtInAx + 14
			aAdd(aArrPrd,{dDtInAx,dDtFiAx})
		Else
			dDtInAx := dDtFiAx + 1
			dDtFiAx := dDtInAx + 14
			If PADL(Day(dDtFiAx),2,"0") $ '28|29|30|31'
				dDtFiAx := LastDay(dDtFiAx)
			EndIf		
			aAdd(aArrPrd,{dDtInAx,dDtFiAx})
		EndIf			
  	Next nContPr
ElseIf nPeriod == "4" // Mensal "Perํodo de 30/31 dias "
	nQtPeri := ABS((dDtFim - dDtIni) / 30)
	lRest	:= IIf(Mod((dDtFim - dDtIni), 30) != 0,.T.,.F.)
	dDtInAx := dDtIni
	For nContPr := 1 To nQtPeri
		/* 
		 * Se Quantidade de Perํodos (Meses) nใo for absoluta, ๚ltimo perํodo deve conter a ๚ltima data com a data final do parโmetro
		 */
		If nContPr == nQtPeri .AND. lRest
			dDtInAx := dDtFiAx + 1
			aAdd(aArrPrd,{aDtInAx,dDtFim})
		/*
		 * Se a Data Inicial for Final de M๊s, Perํodo de 1 dia (somente a data inicial)
		 */
		ElseIf PADL(Day(dDtInAx),2,"0") $ '28|29|30|31' .AND. nContPr == 1
			dDtFiAx := LastDay(dDtInAx)
			aAdd(aArrPrd,{dDtInAx,dDtFiAx})
			dDtInAx := dDtInAx + 1
		Else
			If nContPr > 1
				dDtInAx := FirstDay(dDtFiAx + 1)
			EndIf
			dDtFiAx := LastDay(dDtInAx)
			aAdd(aArrPrd,{dDtInAx,dDtFiAx})
		EndIf			
  	Next nContPr
ElseIf nPeriod == "5" // Bimestral "Periodo de 60 dias "
	nQtPeri := ABS((dDtFim - dDtIni) / 60)
	lRest	:= IIf(Mod((dDtFim - dDtIni), 60) != 0,.T.,.F.)
	dDtInAx := dDtIni
	For nContPr := 1 To nQtPeri
		/* 
		 * Se Quantidade de Perํodos (Bimestres) nใo for absoluta, ๚ltimo perํodo deve conter a ๚ltima data com a data final do parโmetro
		 */
		If nContPr == nQtPeri .AND. lRest
			dDtInAx := dDtFiAx + 1
			aAdd(aArrPrd,{aDtInAx,dDtFim})
		ElseIf nContPr == 1
			If Day(dDtInAx) == 1
				dDtFiAx := LastDay(dDtInAx + 60)
			Else
				dDtFiAx := LastDay( FirstDay(dDtInAx) + 60 )
			EndIf
			aAdd(aArrPrd,{dDtInAx,dDtFiAx})
			dDtInAx := dDtInAx + 1
		Else
			dDtInAx := dDtFiAx + 1
			dDtFiAx := LastDay(Iif(Day(dDtInAx + 60) == 1,(dDtInAx + 60)-1,(dDtInAx + 60)))
			aAdd(aArrPrd,{dDtInAx,dDtFiAx})
		EndIf			
  	Next nContPr  
ElseIf nPeriod == "6" // Trimestral "Periodo de 90 dias "
	nQtPeri := ABS((dDtFim - dDtIni) / 90)
	lRest	:= IIf(Mod((dDtFim - dDtIni), 90) != 0,.T.,.F.)
	dDtInAx := dDtIni
	For nContPr := 1 To nQtPeri
		/* 
		 * Se Quantidade de Perํodos (Trimestres) nใo for absoluta, ๚ltimo perํodo deve conter a ๚ltima data com a data final do parโmetro
		 */
		If nContPr == nQtPeri .AND. lRest
			dDtInAx := dDtFiAx + 1
			aAdd(aArrPrd,{aDtInAx,dDtFim})
		ElseIf nContPr == 1
			If Day(dDtInAx) == 1
				dDtFiAx := LastDay(dDtInAx + 90)
			Else
				dDtFiAx := LastDay( FirstDay(dDtInAx) + 90 )
			EndIf
			aAdd(aArrPrd,{dDtInAx,dDtFiAx})
			dDtInAx := dDtInAx + 1
		Else
			dDtInAx := dDtFiAx + 1
			dDtFiAx := LastDay(Iif(Day(dDtInAx + 90) == 1,(dDtInAx + 90)-1,(dDtInAx + 90)))
			aAdd(aArrPrd,{dDtInAx,dDtFiAx})
		EndIf			
  	Next nContPr
ElseIf nPeriod == "7" // Semestral "Perํodo de 180 dias "
	nQtPeri := ABS((dDtFim - dDtIni) / 180)
	lRest	:= IIf(Mod((dDtFim - dDtIni), 180) != 0,.T.,.F.)
	dDtInAx := dDtIni
	For nContPr := 1 To nQtPeri
		/* 
		 * Se Quantidade de Perํodos (Semestres) nใo for absoluta, ๚ltimo perํodo deve conter a ๚ltima data com a data final do parโmetro
		 */
		If nContPr == nQtPeri .AND. lRest
			dDtInAx := dDtFiAx + 1
			aAdd(aArrPrd,{aDtInAx,dDtFim})
		ElseIf nContPr == 1
			If Day(dDtInAx) == 1
				dDtFiAx := LastDay(dDtInAx + 180)
			Else
				dDtFiAx := LastDay( FirstDay(dDtInAx) + 180 )
			EndIf
			aAdd(aArrPrd,{dDtInAx,dDtFiAx})
			dDtInAx := dDtInAx + 1
		Else
			dDtInAx := dDtFiAx + 1
			dDtFiAx := LastDay(Iif(Day(dDtInAx + 180) == 1,(dDtInAx + 180)-1,(dDtInAx + 180)))
			aAdd(aArrPrd,{dDtInAx,dDtFiAx})
		EndIf			
  	Next nContPr
ElseIf nPeriod == "8" // Anual "Periodo de 365 dias "
	nQtPeri := ABS((Year(dDtFim) - Year(dDtIni)))
	nQtPeri := Iif(nQtPeri < 1,1,nQtPeri)
	dDtInAx := dDtIni
	For nContPr := 1 To nQtPeri
		/* 
		 * Se Quantidade de Perํodos (Semestres) nใo for absoluta, ๚ltimo perํodo deve conter a ๚ltima data com a data final do parโmetro
		 */
		If nContPr == nQtPeri
			dDtInAx	:= CTOD("01/01/"+CVALTOCHAR(Year(dDtInAx)+Iif(nQtPeri > 1,1,0))) 
			aAdd(aArrPrd,{dDtInAx,dDtFim})
		ElseIf nContPr == 1
			dDtFiAx	:= CTOD("31/12/"+CVALTOCHAR(Year(dDtInAx)+Iif(nQtPeri > 1,1,0))) 
			aAdd(aArrPrd,{dDtInAx,dDtFiAx})
		Else
			dDtInAx	:= CTOD("01/01/"+CVALTOCHAR(Year(dDtInAx)+Iif(nQtPeri > 1,1,0))) 
			dDtFiAx	:= CTOD("31/12/"+CVALTOCHAR(Year(dDtInAx)+Iif(nQtPeri > 1,1,0))) 
			aAdd(aArrPrd,{dDtInAx,dDtFiAx})
		EndIf			
  	Next nContPr
EndIf
 
Return(aArrPrd)

//-------------------------------------------------------------------
/*/{Protheus.doc}CtbSegOFi
Avalia o conte๚do do MV_SEGOFI
@author Mauricio Pequim Jr
@since  03/01/2017
@version 12
/*/
//-------------------------------------------------------------------
Function CtbSegOFi()

If cSegOFI == NIL
	cSegOFI := SuperGetMv( "MV_SEGOFI" , .F. , "0" )
Endif

Return cSegOFI
